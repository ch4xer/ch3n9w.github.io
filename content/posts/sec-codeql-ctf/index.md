---
title: CodeQL CTF ä¹‹ Go and don't return
author: ch4ser
date: 2024-12-06T09:03:06+08:00
cagetories:
  - å®‰å…¨ç ”ç©¶
cover:
  image: cover.png
---

# Go and don't return

è¿™é“é¢˜ç›®ä»MinIOçš„ä¸€ä¸ªå·²å…¬å¼€çš„æœªæˆæƒè®¿é—®æ¼æ´å¼€å§‹, é€šè¿‡ç¼–å†™å¹¶æ…¢æ…¢å®Œå–„CodeQLè¯­å¥æ¥æ£€æµ‹è¯¥æ¼æ´åœ¨ä»£ç ä¸­çš„ä½ç½®. éšç€é¢˜ç›®çš„è¿›è¡Œ, é¢˜ç›®å°†ç›®æ ‡å°†æ‰©å±•åˆ°æ­¤ç±»æ¼æ´çš„å„ç§å˜ç§, éš¾åº¦é€æ¸åŠ æ·±, éå¸¸é€‚åˆç”¨æ¥å…¥é—¨å’Œå­¦ä¹ . æœ¬æ–‡æ˜¯ç”¨æ¥è®°å½•æˆ‘åœ¨é˜…è¯»å®˜æ–¹é¢˜è§£æ—¶çš„æ€è€ƒè¿‡ç¨‹, å› ä¸ºæˆ‘åœ¨Part2å°±å¡ä½å•¦hhhhhhh.

[ğŸ”—åŸé¢˜ç›®é“¾æ¥](https://securitylab.github.com/ctf/go-and-dont-return/)

[ğŸ”—å®˜æ–¹å‚è€ƒé¢˜è§£é“¾æ¥](https://securitylab.github.com/ctf/go-and-dont-return/answers/)

åœ¨å¼€å§‹ä¹‹å‰, å¯ä»¥æŸ¥çœ‹ä¸€ä¸‹è¯¥æ¼æ´çš„[ç»†èŠ‚](https://github.com/minio/minio/commit/4cd6ca02c7957aeb2de3eede08b0754332a77923) å’Œ [æè¿°](https://github.com/minio/minio/security/advisories/GHSA-xv4r-vccv-mg4w), å‘ç°è¯¥æ¼æ´çš„åŸç†å…¶å®ååˆ†ç®€å•, å°±æ˜¯åœ¨ `if s3Err != ErrNone` åˆ¤æ–­ä¹‹åæ²¡æœ‰å°†ä¸åˆæ³•çš„æ§åˆ¶æµè¿”å›, è€Œæ˜¯ä»»ç”±å®ƒç»§ç»­è¿›å…¥æ¥ä¸‹å»çš„`claims, s3Err := checkClaimsFromToken(r, cred)`, å¯¼è‡´æ ¡éªŒå¤±è´¥.

å…·ä½“çš„æ¼æ´åˆ©ç”¨å¯ä»¥çœ‹[è¿™é‡Œ](https://github.com/cokeBeer/go-cves/blob/main/CVE-2020-11012/CVE-2020-11012.md)

## Part 1: Letâ€™s catch the bug!

### 1.1: Finding references to ErrNone

> Write the query that finds all identifiers named ErrNone. You will find in the documentation the relevant object types to query. Your query should return 231 results.

```ql

import go

from Ident i
where i.getName() = "ErrNone" //Identå°±æ˜¯æ ‡è¯†ç¬¦
select i
```

è¿è¡Œåå°±å¯ä»¥æ£€ç´¢å‡ºErrNoneå‡ºç°è¿‡çš„æ‰€æœ‰ä½ç½®.

### 1.2: Finding equality tests against ErrNone

In this next step, write a query to find all equality test expressions where one of the operands is an identifier called ErrNone. Your query should give you 158 results.

```ql
import go

from EqualityTestExpr e
where e.getAnOperand().(Ident).getName() = "ErrNone"
select e
```

`EqualityTestExpr`æ˜¯ä¸€ä¸ªç­‰å¼æµ‹è¯•, ä¸º`==`æˆ–`!=`, å®ƒæ˜¯ä¸€ä¸ªExpression.

åœ¨CodeQLä¸­, Expressionæ˜¯èƒ½è®¡ç®—ä¸ºä¸€ç»„å€¼å¹¶ä¸”å…·æœ‰ç±»å‹çš„è¯­å¥, æ¯”å¦‚`1+1`, `"QL"`, `[1,2,3,4,5]`.

ä¸ä¹‹å¯¹åº”çš„æ˜¯Statement, ä¾‹å¦‚tryè¯­å¥, ifè¯­å¥ç­‰ç­‰. **statementæ²¡æœ‰è¿”å›å€¼, è€Œexpressionæ˜¯æœ‰è¿”å›å€¼çš„.**

`e.getAnOperand().(Ident).getName()` è¿™å—æ˜¯å…ˆä»`EqualityTestExpr`ä¸­è·å–æ“ä½œæ•°, ç„¶åè¿›è¡Œç±»å‹è½¬æ¢åè¿›è¡ŒæŸ¥çœ‹.

### 1.3: Finding if-blocks making such a test

> Write a query that finds all if statements, where the condition is an equality test similar as found in step 1.2. Your query should give you 133 results.

```ql
import go

from IfStmt s
where s.getCond().(EqualityTestExpr).getAnOperand().(Ident).getName() = "ErrNone"
select s
```

ä»ä¹‹å‰çš„EqualityTestExpræ›¿æ¢ä¸ºäº†IfStmt, ç¼©å°äº†èŒƒå›´.

### 1.4: Finding return statements

> Write a query that finds all return statements. Your query should give 10,651 results.

```ql
import go

from ReturnStmt r
select r
```

å¯ä»¥çœ‹å‡ºæ¥codeqlæ”¯æŒè¯†åˆ«çš„statementç§ç±»è¿˜æŒºå¤šçš„.

### 1.5: Finding if-blocks without return statements

> Write a query that finds all if-blocks that donâ€™t contain return statements in their then branch. Your query should return 3541 results. Remember, we are doing baby steps! We just care about the then branch for now!

è¦æ‰¾æ‰€æœ‰if-blockä¸­ä¸åŒ…å«è¿”å›è¯­å¥çš„.

```ql
import go

from IfStmt i
where not i.getThen().getAStmt() instanceof ReturnStmt
select i
```

è¿™é‡Œ`i.getThen().getAStmt()`æ˜¯ä»içš„thenåˆ†æ”¯ä¸­è·å–ä¸€ä¸ªstatement, æŒ‰ç†æ¥è¯´å¹¶ä¸ç¡®å®šè¿™ä¸ªstatementæ˜¯å¦**æœ¬åº”è¯¥**æ˜¯è¿”å›è¯­å¥, æ¯•ç«Ÿå³ä¾¿å­˜åœ¨ReturnStmt, ä¹Ÿå¯èƒ½æ²¡æœ‰è¢«`getAStmt`è·å–åˆ°. ç„¶è€Œå…³é”®åœ¨äºè¿™ä¸ª`not`, å®ƒå’Œä¸ç­‰äºæœ‰ç€å®Œå…¨ä¸åŒçš„å«ä¹‰:

- `A != B` holds if there is a pair of values (one from A and one from B) that are different.
- `not A = B` holds if it is not the case that there is a pair of values that are the same. In other words, **A and B have no values in common**.

è‡³æ­¤å°±æ¯”è¾ƒæ¸…æ¥šäº†

### 1.6: Putting it all together

> Ok, time to find our bug! Combine steps 1.5 and 1.3 and write a query that finds the if-blocks testing for equality to ErrNone with no return.
> You should get a total of 7 results. Check that the bug we're looking for is one of them

```ql
import go

from IfStmt i
where
i.getCond().(EqualityTestExpr).getAnOperand().(Ident).getName() = "ErrNone"
and not i.getThen().getAStmt() instanceof ReturnStmt
select i
```

å°†ä¸Šé¢çš„ç»“åˆå°±å¯ä»¥äº†.

è‡³æ­¤, å·²ç»å¯ä»¥æ£€æµ‹å‡ºæºå¸¦æ¼æ´ç‰¹å¾çš„ifä»£ç æ‰€åœ¨çš„ä½ç½®äº†.

## Part 2: Improving the precision

ä¸Šè¿°codeqlçš„æŸ¥è¯¢ç»“æœä¸­å­˜åœ¨ä¸€äº›è¯¯æŠ¥. ä¸ºäº†å‡å°‘è¯¯æŠ¥, è€ƒè™‘ä½¿ç”¨æ›´ç²¾ç¡®çš„æ•°æ®æµåˆ†ææ¥æ£€æŸ¥`isReqAuthenticated`çš„è¿”å›å€¼.

![isReqAuthenticated](./isReqAuthenticated.png)


### 2.1: Find conditionals that are fed from calls to isReqAuthenticated

> Write a data flow configuration that tracks data flowing from any call to isReqAuthenticated to any equality test operand. Your query must select all equality tests -- Type: DataFlow::EqualityTestNode -- where the operand is a sink of the above configuration. 

> This gives us 64 potentially interesting conditionals to investigate. Note many of them are not direct calls to isReqAuthenticated, instead they test the result of some intermediate function which in turn calls isReqAuthenticated. The CodeQL global data flow analysis feature allows us to detect those.

```ql
import go

class AuthTestConfig extends DataFlow::Configuration {

  AuthTestConfig() { this = "auth-test-config" }

  override predicate isSource(DataFlow::Node source) {
    source = any(DataFlow::CallNode cn |
      cn.getTarget().hasQualifiedName("github.com/minio/minio/cmd", "isReqAuthenticated")
    ).getResult()
  }

  override predicate isSink(DataFlow::Node sink) {
    sink = any(DataFlow::EqualityTestNode n).getAnOperand()
  }

}

from AuthTestConfig config, DataFlow::Node sink, DataFlow::EqualityTestNode comparison
where config.hasFlow(_, sink) and comparison.getAnOperand() = sink
select comparison
```

å“‡å¡, ~~æ˜¯ä¼ è¯´ä¸­çš„æ±¡ç‚¹åˆ†æ!~~ å…¶å®ä¸¥æ ¼æ„ä¹‰ä¸Šä¸ç®—æ˜¯, è¿™ä¸ªç®—æ˜¯æ•°æ®æµè·Ÿè¸ª.

`AuthTestConfig`å®šä¹‰äº†æ•°æ®æµé…ç½®, å…¶ä¸­`isSource`å’Œ`isSink`åˆ†åˆ«å®šä¹‰äº†æ±¡ç‚¹åˆ†æä¸­çš„æ•°æ®æµèµ·ç‚¹Sourceå’Œæ•°æ®æµç»ˆç‚¹Sink. è¿™é‡ŒSourceä¸ºè°ƒç”¨`isReqAuthenticated`å‡½æ•°çš„èŠ‚ç‚¹, Sinkæ˜¯æ¯”è¾ƒæµ‹è¯•èŠ‚ç‚¹ä¸­çš„æ“ä½œæ•°.

æ ¹æ®[æ–‡æ¡£](https://codeql.github.com/docs/ql-language-reference/expressions/#any), `any`è¿™ç©æ„çš„ç”¨æ³•å¦‚ä¸‹

```
any(<variable declarations> | <formula> | <expression>)
```

`<formula>`æ˜¯å¯¹variableçš„è¦æ±‚, å¯¹äºæ¯ä¸€ä¸ªæ»¡è¶³è¦æ±‚çš„å˜é‡éƒ½ä¼šè¿”å›ä¸€ä¸ª`<expressions>`, å¦‚æœæ²¡æœ‰`<expressions>`å°±è¿”å›variableæœ¬èº«, å› æ­¤å¯èƒ½ä¼šè¿”å›å¤šä¸ª.

é‚£ä¹ˆé—®é¢˜æ¥äº†, sourceæˆ–è€…sinkæ˜¯æ€ä¹ˆå’Œanyçš„å¤šä¸ªè¿”å›å€¼è¿›è¡Œæ¯”è¾ƒçš„å‘¢? å…¶å®ç­”æ¡ˆåœ¨[æ–‡æ¡£](https://codeql.github.com/docs/ql-language-reference/formulas/#equality-1)ä¸­å·²ç»æè¿°äº†:

> For expressions A and B, the formula A = B holds if there is a pair of valuesâ€”one from A and one from Bâ€”that are the same. In other words, A and B have at least one value in common. For example, [1 .. 2] = [2 .. 5] holds, since both expressions have the value 2.

å› æ­¤, æ ¹æ®æ–‡æ¡£, `1 = [1..2]` æ˜¯æˆç«‹çš„, é‚£ä¹ˆ`isSource`å’Œ`isSink`ä¸­çš„æ¯”è¾ƒä¹Ÿæ˜¯æˆç«‹çš„.

æœ€å, `config.hasFlow(_, sink)` ä¸­å°†sourceéƒ¨åˆ†è®¾ç½®ä¸º`_`, å› ä¸ºæˆ‘ä»¬çš„æŸ¥è¯¢ä¸­å¹¶ä¸åŒ…å«source, è€Œ`comparison.getOperand() = sink`ä¹Ÿæ˜¯æ–¹ä¾¿å»æŸ¥è¯¢å‡ºç›¸åº”çš„æ¯”è¾ƒæµ‹è¯•èŠ‚ç‚¹.


### 2.2: Find the true bug!

> We can now put this dataflow query together with our query from step 1.6, and find all if statements that

> - Are one of the equality tests returned in 2.1
> - Are testing equality against ErrNone
> - Do not contain a return statement in their then branch


```ql
import go

class AuthTestConfig extends DataFlow::Configuration {

  AuthTestConfig() { this = "auth-test-config" }

  override predicate isSource(DataFlow::Node source) {
    source = any(DataFlow::CallNode cn |
      cn.getTarget().hasQualifiedName("github.com/minio/minio/cmd", "isReqAuthenticated")
    ).getResult()
  }

  override predicate isSink(DataFlow::Node sink) {
    sink = any(DataFlow::EqualityTestNode n).getAnOperand()
  }

}

EqualityTestExpr getAnAuthCheck() {
  exists(AuthTestConfig config, DataFlow::Node sink, DataFlow::EqualityTestNode comparison |
    config.hasFlow(_, sink) and comparison.getAnOperand() = sink |
    result = comparison.asExpr()
  )
}

from IfStmt i
where
i.getCond() = getAnAuthCheck() and
i.getCond().(EqualityTestExpr).getAnOperand().(Ident).getName() = "ErrNone"
and not i.getThen().getAStmt() instanceof ReturnStmt
select i
```

å°†ä¹‹å‰çš„éƒ¨åˆ†ç»“åˆèµ·æ¥å°±å¯ä»¥äº†. è¿™é‡Œæ–°å‡ºç°äº†ä¸€ä¸ª`exists`, å…¶ç”¨æ³•ä¸º:

```
exists(<variable declarations> | <formula>)
```

å½“å­˜åœ¨ä¸€ä¸ªå˜é‡ä½¿å¾—formulaæˆç«‹, å…¬å¼æˆç«‹, è€Œformulaå¯ä»¥æœ‰å¤šä¸ª, å³

```
exists(<variable declarations> | <formula 1> | <formula 2> |...| <formula n>)
```

ç­‰ä»·äº

```
exists(<variable declarations> | <formula 1> and <formula 2> and...and <formula n>)
```

å¦‚æœè‡³å°‘å­˜åœ¨ä¸€ç»„variablesä½¿å¾—å…¨éƒ¨formulaéƒ½æ»¡è¶³, é‚£ä¹ˆå…¬å¼æˆç«‹.

è€Œé¢˜è§£ä¸­çš„è¿™ç§å†™æ³•, ç›¸å½“äºç­‰å‰é¢æ‰€æœ‰çš„formulaéƒ½æ»¡è¶³çš„æ—¶å€™, ä¸´æ—¶å˜é‡resultä¼šè¢«èµ‹å€¼ `comparison.asExpr()` , è€Œresultåˆæ˜¯ `getAnAuthCheck` çš„è¿”å›å€¼, å› æ­¤å°±å˜æˆ: è¿”å›ä¸€ç»„æ»¡è¶³è¿ç®—å•å…ƒæ˜¯æ±¡ç‚¹æºçš„ `EqualityTestExpr`.


## Part 3: Expanding the query

è¿™éƒ¨åˆ†è¦æ‰©å¤§èŒƒå›´, ä¸å†å±€é™äºMinIOäº†, ä½¿ç”¨çš„æ£€æµ‹å¯¹è±¡æ˜¯è¿™é“é¢˜ç›®é™„å¸¦çš„database.

### 3.1: Conditional polarity

> You might have noticed this in step 1.6: our code looking for equality tests encompases both x == ErrNone and x != ErrNone and checks the then block in both cases. This is wrong. It ought to check the "then" or "else" case of an if block, depending on which form of conditional is used. Modify your query to fix this problem. Your query should be able to detect all bad examples in conditionalPolarities.go.
> Hint: Check out the predicate EqualityTestExpr.getPolarity

éœ€è¦æ ¹æ®`x==ErrNone`å’Œ`x!=ErrNone`çš„ä¸åŒæƒ…å†µè¿›è¡Œä¸åŒæ£€æŸ¥.

```ql
import go

class AuthTestConfig extends DataFlow::Configuration {

  AuthTestConfig() { this = "auth-test-config" }

  override predicate isSource(DataFlow::Node source) {
    source = any(DataFlow::CallNode cn |
      cn.getTarget().hasQualifiedName("github.com/minio/minio/cmd", "isReqAuthenticated") or
      // Note new source function:
      cn.getTarget().hasQualifiedName("github.com/github/codeql-ctf-go-return", "errorSource")
    ).getResult()
  }

  override predicate isSink(DataFlow::Node sink) {
    sink = any(DataFlow::EqualityTestNode n).getAnOperand()
  }

}

EqualityTestExpr getADirectAuthCheck(boolean polarity) {
  exists(AuthTestConfig config, DataFlow::Node sink, DataFlow::EqualityTestNode comparison |
    config.hasFlow(_, sink) and comparison.getAnOperand() = sink |
    result = comparison.asExpr() and
    polarity = result.getPolarity()
  )
}

/**
 * Given `ifStmt`'s condition compares some `x` against `ErrNone` with `polarity` (true means checking
 * equality; false checking inequality), return the block reached when `x != ErrNone`.
 */
BlockStmt getErrorBranch(IfStmt ifStmt, boolean polarity) {
  polarity = [true, false] and
  if polarity = true then result = ifStmt.getElse() else result = ifStmt.getThen()
}

from IfStmt i, boolean testPolarity
where
i.getCond() = getADirectAuthCheck(testPolarity) and
i.getCond().(EqualityTestExpr).getAnOperand().(Ident).getName() = "ErrNone"
and not getErrorBranch(i, testPolarity).getAStmt() instanceof ReturnStmt
select i
```

é‡ç‚¹æ˜¯`if polarity = true then result = ifStmt.getElse() else result = ifStmt.getThen()`è¿™ä¸€å¥.

å¦å¤–`polarity = [true, false]`è¿™ä¸ªæ˜¯å¯¹å˜é‡çš„é™åˆ¶, åœ¨[æ–‡æ¡£](https://codeql.github.com/docs/ql-language-reference/predicates/#binding-behavior)ä¸­æœ‰ç›¸å…³çš„æè¿°

> It must be possible to evaluate a predicate in a finite amount of time, so the set it describes is not usually allowed to be infinite. In other words, a predicate can only contain a finite number of tuples.

å¦‚æœæ²¡æœ‰`polarity = [true, false]`, è™½ç„¶ç†è®ºä¸Šæ¥è¯´booleanå˜é‡ä¹Ÿåªæœ‰ä¸¤ä¸ªå€¼, ä½†æ˜¯ç»Ÿä¸€èµ·è§è¿˜æ˜¯éœ€è¦åšé™åˆ¶.

### 3.2: More blocks

> Let's detect more blocks that must return. For example, our query fails to detect a return statement in an else branch, and there are other such cases that we need to handle, such as cascading else or switch/case. Modify your query to find more blocks that donâ€™t return. Your query should be able to detect all bad examples in moreWaysToReturn.go.
> Hints:
>   While we could recursively inspect the control-flow structures inside the if block, it may help to use the control-flow graph. Check the documentation of the class IR::ReturnInstruction, a control-flow graph node corresponding to a return statement, and the getAPredecessor() / getASuccessor() methods of its superclass ControlFlow::Node, which traverse control-flow graph edges.
>   A passing or failing if test is always followed by a ConditionGuardNode that indicates which branch was taken.

> Tip: Try creating a temporary query such as the one below to get an idea what the control flow graph looks like.

```ql
from ControlFlow::Node pred, ControlFlow::Node succ 
    where succ = pred.getASuccessor() // you can also restrict `pred` to come from a particular source file
    select pred, succ
```

è¦å¯»æ‰¾æ›´å¤šåˆ†æ”¯è¯­å¥ä¸­çš„æ²¡æœ‰è¿”å›çš„éƒ¨åˆ†.


```ql
import go

class AuthTestConfig extends DataFlow::Configuration {

  AuthTestConfig() { this = "auth-test-config" }

  override predicate isSource(DataFlow::Node source) {
    source = any(DataFlow::CallNode cn |
      cn.getTarget().hasQualifiedName("github.com/minio/minio/cmd", "isReqAuthenticated") or
      cn.getTarget().hasQualifiedName("github.com/github/codeql-ctf-go-return", "errorSource")
    ).getResult()
  }

  override predicate isSink(DataFlow::Node sink) {
    sink = any(DataFlow::EqualityTestNode n).getAnOperand()
  }

}

EqualityTestExpr getAnAuthCheck() {
  exists(AuthTestConfig config, DataFlow::Node sink, DataFlow::EqualityTestNode comparison |
    config.hasFlow(_, sink) and comparison.getAnOperand() = sink |
    result = comparison.asExpr()
  )
}

ReturnStmt getAReturnStatementInBlock(BlockStmt b) {
  result = b.getAChild*()
}

predicate mustReachReturnInBlock(ControlFlow::Node node, BlockStmt b) {
  node.(IR::ReturnInstruction).getReturnStmt() = getAReturnStatementInBlock(b) or
  forex(ControlFlow::Node succ | succ = node.getASuccessor() | mustReachReturnInBlock(succ, b))
}

from IfStmt i, ControlFlow::ConditionGuardNode ifSucc
where
i.getCond() = getAnAuthCheck() and
i.getCond().(EqualityTestExpr).getAnOperand().(Ident).getName() = "ErrNone" and
ifSucc.ensures(DataFlow::exprNode(i.getCond()), true) and
not mustReachReturnInBlock(ifSucc, i.getThen())
select i
```

`ifSucc.ensures(DataFlow::exprNode(i.getCond()), true)` æ¶‰åŠåˆ°äº†æ§åˆ¶æµèŠ‚ç‚¹`ifSucc`, è¿™æ¡è¯­å¥çš„æ„æ€æ˜¯ **ç¡®ä¿`i.getCond()`çš„æ¡ä»¶æˆç«‹**, æˆç«‹ä¹‹åæ‰å¯¹thenåˆ†æ”¯çš„è¯­å¥è¿›è¡Œåˆ†æ.

å…³äºConditionGuardNode, [æ–‡æ¡£](https://codeql.github.com/docs/codeql-language-guides/codeql-library-for-go/#condition-guard-nodes)ä¸­çš„æœ‰ç›´è§‚çš„å®ä¾‹.

å¯¹äºä»¥ä¸‹ä»£ç 

```go
x := 0
if p != nil {
  x = p.f
}
return x
```

å…¶ConditionGuardNodeä¸ºå›¾ä¸­çš„`p != nil is true`å’Œ`p != nil is false`.

---

å½“å˜é‡è°ƒç”¨è°“è¯(predicate)çš„æ—¶å€™, å¦‚æœå­˜åœ¨ç»“æœ, é‚£ä¹ˆè¿™ç§è°ƒç”¨æ˜¯ä¸€ä¸ªè¡¨è¾¾å¼, å¦åˆ™å°±æ˜¯ä¸€ä¸ªå…¬å¼. è¿”å›ç»“æœçš„è°“è¯å¯ä»¥åœ¨ç»è¿‡é€»è¾‘åˆ¤æ–­åè¿”å›å¤šä¸ªç»“æœ, ä¾‹å¦‚

```ql
string getANeighbor(string country) {
  country = "France" and result = "Belgium"
  or
  country = "France" and result = "Germany"
  or
  country = "Germany" and result = "Austria"
  or
  country = "Germany" and result = "Belgium"
}
```

å¯¹äºä¸Šè¿°predicate, å¦‚æœ`country`ä¸º"Germany", é‚£ä¹ˆè¿”å›å€¼ä¸ºAustriaå’ŒBelgium, è¿™ç‚¹å’Œå¹³æ—¶ç”¨çš„ç¼–ç¨‹è¯­è¨€åŒºåˆ«æŒºå¤§çš„.

åŒæ ·çš„, `b.getAChild()`æ˜¯ä¸€ä¸ªå¸¦ç»“æœçš„è°“è¯è°ƒç”¨, ä¼šè¿”å›å¤šä¸ªå­èŠ‚ç‚¹.

æ¥ä¸‹æ¥æ˜¯ä»£è¡¨é—­åŒ…ä¼ é€’çš„`*`å’Œ`+`, è¿™ä¸¤ä¸ªéƒ½æœ‰è¿­ä»£çš„åŠŸèƒ½, ä¾‹å¦‚`ReturnStmt getAChild*()` ç­‰ä»·äº

```ql
ReturnStmt getOneChild() {
  result = this
  or
  result = this.getAChild().getOneChild()
}
```

è€Œ`ReturnStmt getAChild+()` ç­‰ä»·äº

```ql
ReturnStmt getOneChild() {
  result = this.getAChild()
  or
  result = this.getAChild().getOneChild()
}
```

ä¹Ÿå°±æ˜¯è¯´`*`ç›¸æ¯”è¾ƒ`+`è¿˜åŒ…å«äº†è‡ªèº«.

é‚£ä¹ˆ`getAReturnStatementInBlock`çš„ç»“æœå°±æ˜¯å½“å‰èŠ‚ç‚¹åŠ ä¸Šå½“å‰èŠ‚ç‚¹ç»è¿‡é€’å½’åçš„æ‰€æœ‰å­èŠ‚ç‚¹.

é‚£ä¹ˆå­èŠ‚ç‚¹çš„é›†åˆè¦æ€ä¹ˆå’Œ`node.(IR::ReturnInstruction).getReturnStmt()`è¿›è¡Œæ¯”è¾ƒ? æ ¹æ®[æ–‡æ¡£](https://codeql.github.com/docs/ql-language-reference/formulas/#equality-1)ä¸­æ‰€æè¿°çš„:

> For expressions A and B, the formula A = B holds if there is a pair of valuesâ€”one from A and one from Bâ€”that are the same. In other words, A and B have at least one value in common. For example, [1 .. 2] = [2 .. 5] holds, since both expressions have the value 2.

é‚£ä¹ˆå°±ä¸éš¾ç†è§£äº†, åªè¦`node.(IR::ReturnInstruction).getReturnStmt()`çš„å€¼å’Œå…¶ä¸­ä¸€ä¸ªç›¸åŒ, `mustReachReturnInBlock`å°±ä¼šæˆç«‹, ä¸ç„¶å°±è¿›å…¥`forex`éƒ¨åˆ†.

`forex`çš„ç”¨æ³•, æ ¹æ®[æ–‡æ¡£](https://codeql.github.com/docs/ql-language-reference/formulas/#forex), å¯ä»¥çœ‹ä½œæ˜¯`forall` å’Œ `exists` çš„ç»“åˆ. ä¸‰è€…çš„å®šä¹‰å¦‚ä¸‹

```
forall(<variable declarations> | <formula 1> | <formula 2>)
forex(<variable declarations> | <formula 1> | <formula 2>) ç­‰ä»·äº forall(<vars> | <formula 1> | <formula 2>) and exists(<vars> | <formula 1> | <formula 2>)
```

- `forall`: å½“æ¯ä¸€ä¸ªè®©formula1 æˆç«‹çš„å˜é‡åœ¨formula2æˆç«‹çš„æ—¶å€™, å…¬å¼æˆç«‹
- `forex`: å’Œ`forall`ç±»ä¼¼, ä½†æ˜¯æ’é™¤äº†`ä¸å­˜åœ¨æ»¡è¶³formula1çš„å˜é‡çš„æƒ…å†µ`, å› ä¸ºè¿™ç§æƒ…å†µä¸‹`formula2`ä¼šæ— æ¡ä»¶æˆç«‹.

é‚£ä¹ˆä»£ç ä¸­`forex`çš„æ„æ€å°±æ˜¯: **å¯¹äºnodeçš„successor, è¦æ±‚successorå­˜åœ¨, å¹¶ä¸”æ¯ä¸€ä¸ªsuccessoræ»¡è¶³mustReachReturnInBlock, ä¹Ÿå°±æ˜¯è¯´æ¯ä¸€ä¸ªsuccessoréƒ½å­˜åœ¨ReturnStmt**

---

åœ¨æŸ¥é˜…æ–‡æ¡£çš„æ—¶å€™å¯ä»¥æ³¨æ„ç±»çš„`supertypes`éƒ½æœ‰å“ªäº›, åŒ…æ‹¬`Direct supertypes` å’Œ `Indirect supertypes`. æ‰€è°“`Indirect supertypes`å°±æ˜¯æŒ‡, `supertype`åœ¨ç»å†äº†å¤šæ¬¡ç»§æ‰¿ä¹‹åæ‰èƒ½å˜æˆè¯¥ç±», è€Œ`direct supertypes` æ˜¯ç›´æ¥ç»§æ‰¿çš„. è¿™äº›`supertypes`éƒ½å¯ä»¥é€šè¿‡ç±»å‹è½¬æ¢å˜æˆè¿™ä¸ªç±».


### 3.3: Wrapped conditionals

> Now we can have cases where our equality test against ErrNone is no longer directly used in a conditional statement, but is instead wrapped inside a utility function. Modify your query to handle this case. Your query should be able to detect all bad examples in wrapperFunctions.go.

Hint:

- You can have several layers in your wrap!
- Check out the predicates CallExpr::getTarget(), DataFlow::CallNode::getTarget() and Function::getFuncDecl() to navigate between a callsite and its callee.

ç›¸æ¯”è¾ƒä¹‹å‰çš„ç›´æ¥æŸ¥è¯¢ifè¯­å¥ä¸­çš„å†…å®¹, è¿™é‡Œè¦æ±‚é¢å¤–è¦†ç›–ä½¿ç”¨å‡½æ•°è¿”å›å€¼è¿›è¡Œåˆ¤æ–­çš„æƒ…å†µ, ä¾‹å¦‚

```go
if succeeded(errorSource()) {
		doSomething()
} else {
		// Bad: despite an error, we carry on to execute doSomething()
		insteadOfReturn()
}
```

```ql
import go

class AuthTestConfig extends DataFlow::Configuration {

  AuthTestConfig() { this = "auth-test-config" }

  override predicate isSource(DataFlow::Node source) {
    source = any(DataFlow::CallNode cn |
      cn.getTarget().hasQualifiedName("github.com/minio/minio/cmd", "isReqAuthenticated") or
      // Note new source function:
      cn.getTarget().hasQualifiedName("github.com/github/codeql-ctf-go-return", "errorSource")
    ).getResult()
  }

  override predicate isSink(DataFlow::Node sink) {
    sink = any(DataFlow::EqualityTestNode n).getAnOperand()
  }

}

EqualityTestExpr getADirectAuthCheck(boolean polarity) {
  exists(AuthTestConfig config, DataFlow::Node sink, DataFlow::EqualityTestNode comparison |
    config.hasFlow(_, sink) and comparison.getAnOperand() = sink |
    result = comparison.asExpr() and
    result.getAnOperand().(Ident).getName() = "ErrNone" and
    polarity = result.getPolarity()
  )
}

CallExpr getACheckCall(boolean polarity, FuncDecl target, Expr innerCheck) {
  innerCheck = getAnAuthCheck(polarity) and
  target = result.getTarget().getFuncDecl() and
  forex(DataFlow::ResultNode rn | rn.getRoot() = target | rn.asExpr() = innerCheck)
}

Expr getAnAuthCheck(boolean polarity) {
  result = getADirectAuthCheck(polarity) or
  result = getACheckCall(polarity, _, _)
}

/**
 * Given `ifStmt`'s condition compares some `x` against `ErrNone` with `polarity` (true means checking
 * equality; false checking inequality), return the block reached when `x != ErrNone`.
 */
BlockStmt getErrorBranch(IfStmt ifStmt, boolean polarity) {
  polarity = [true, false] and
  if polarity = true then result = ifStmt.getElse() else result = ifStmt.getThen()
}

from IfStmt i, boolean testPolarity
where
i.getCond() = getAnAuthCheck(testPolarity)
and not getErrorBranch(i, testPolarity).getAStmt() instanceof ReturnStmt
select i
```

`getErrorBranch` æ²¡ä»€ä¹ˆå¥½è¯´çš„, å°±æ˜¯æ ¹æ®`polarity`é€‰æ‹©elseåˆ†æ”¯æˆ–è€…thenåˆ†æ”¯.

ä½†æ˜¯`getAnAuthCheck`å°±ä¸åŒäº†, è¿™é‡Œç”¨åˆ°äº†ä¸€ä¸ªç›¸å½“é«˜çº§çš„å†™æ³•: ç›¸äº’é€’å½’(Mutal Recursion), è¿™ä¼šå½¢æˆè°“è¯ä¹‹é—´çš„ä¾èµ–é“¾æ¡, å¯ä»¥çœ‹[æ–‡æ¡£](https://codeql.github.com/docs/ql-language-reference/recursion/#mutual-recursion) ç»™å‡ºçš„ä¸€ä¸ªä¾‹å­.

```ql
int getAnEven() {
  result = 0
  or
  result <= 100 and result = getAnOdd() + 1
}

int getAnOdd() {
  result = getAnEven() + 1
}

select getAnEven()
```

åœ¨è¿™ä¸ªä¾‹å­ä¸­`getAnEven`å’Œ`getAnOdd`ä¹‹é—´å½¢æˆäº†è°“è¯ä¾èµ–é“¾æ¡, codeqlä¼šæŒç»­é€’å½’åˆ°é™åˆ¶æ¡ä»¶åè¿”å›0åˆ°100çš„å¶æ•°.

`getAnAuthCheck`å’Œ`getACheckCall`ä¹‹é—´ä¹Ÿå­˜åœ¨ç›¸äº’é€’å½’çš„å…³ç³». `getAnAuthCheck` é¦–å…ˆæ ¹æ®`polarity`çš„å€¼å»é€‰æ‹©`xxx==ErrNone`æˆ–`xxx!=ErrorNone`è¿™ä¸¤ç§sinkå¹¶è½¬åŒ–ä¸ºè¡¨è¾¾å¼, å’Œä¹‹å‰çš„å·®ä¸å¤š, è€Œ`getACheckCall`ä¸­, é¦–å…ˆä¼šåè¿‡æ¥é€’å½’è·å–`getAnAuthCheck`çš„ç»“æœ.

å®šä¹‰`target`ä¸ºå‡½æ•°çš„å£°æ˜, åŒæ—¶ä¹Ÿå¯¹`CallExpr`ç±»å‹çš„`result`ä½œäº†éšå¼çš„å®šä¹‰.

æ¥ä¸‹æ¥, `forex`å¯¹æ¯ä¸€ä¸ª`DataFlow::ResultNode` è¿›è¡Œæ£€æŸ¥, `DataFlow::ResultNode` åœ¨[æ–‡æ¡£](https://codeql.github.com/codeql-standard-libraries/go/semmle/go/dataflow/internal/DataFlowNodes.qll/type.DataFlowNodes$Public$ResultNode.html)ä¸­æè¿°ä¸º:

> This can either be a node corresponding to an expression in a return statement, or a node representing the current value of a named result variable at the exit of the function.

è€Œ`getRoot`èƒ½å¤Ÿè·å–èŠ‚ç‚¹æ‰€å±çš„å‡½æ•°, è¿™ä¸ª`forex`çš„æ„æ€å°±æˆä¸ºäº† **è¦æ±‚æ¯ä¸€ä¸ªResultNodeæ‰€å±çš„å‡½æ•°éƒ½ä¸targetä»£è¡¨çš„å‡½æ•°å¯¹åº”, åŒæ—¶ResultNodeçš„è¡¨è¾¾å¼æ˜¯getAnAuthCheckçš„ç»“æœ, ä¹Ÿå°±æ˜¯innerCheck**

é‚£ä¹ˆ`innerCheck`ä¹Ÿå°±æ˜¯`getAnAuthCheck(polarity)`, ä»£è¡¨çš„å°±æ˜¯**æ¡ä»¶åˆ¤æ–­çš„sink, ä»¥åŠæ¡ä»¶åˆ¤æ–­ä¸­æ‰€ä½¿ç”¨çš„å­˜åœ¨æ¡ä»¶åˆ¤æ–­sinkçš„å‡½æ•°**.

### 3.4: More conditionals

> Our code works for simple equality tests, but there are cases where this test is part of a bigger test with conditionals involving !, &&, ||, that are not currently accounted for in our query. Improve your query to handle these cases. Your query should be able to detect all bad examples in logicalOperators.go.
> Hint: Check out ControlFlow::ConditionGuardNode. This node flags a point in a control-flow graph where a particular test is known to have passed or failed, including those nested within the short-circuiting binary logical operators &&, ||. Its predicate ensures can already analyse some boolean expression structure. Even if you cannot use it directly, the implementation of ensures may be a useful inspiration for your solution. See the hints for Step 3.2 for more information about the control-flow graph.

ä¹Ÿå°±æ˜¯è¯´è¦å¯¹!x, x||y, x && y, (x) è¿™äº›æƒ…å†µè¿›è¡Œå¤„ç†.

```ql
import go

class AuthTestConfig extends DataFlow::Configuration {

  AuthTestConfig() { this = "auth-test-config" }

  override predicate isSource(DataFlow::Node source) {
    source = any(DataFlow::CallNode cn |
      cn.getTarget().hasQualifiedName("github.com/minio/minio/cmd", "isReqAuthenticated") or
      // Note new source function:
      cn.getTarget().hasQualifiedName("github.com/github/codeql-ctf-go-return", "errorSource")
    ).getResult()
  }

  override predicate isSink(DataFlow::Node sink) {
    sink = any(DataFlow::EqualityTestNode n).getAnOperand()
  }

}

EqualityTestExpr getADirectAuthCheck(boolean polarity) {
  exists(AuthTestConfig config, DataFlow::Node sink, DataFlow::EqualityTestNode comparison |
    config.hasFlow(_, sink) and comparison.getAnOperand() = sink |
    result = comparison.asExpr() and
    polarity = result.getPolarity()
  )
}

Expr getAnAuthCheck(Boolean noError, EqualityTestExpr test) {
  result = getADirectAuthCheck(noError) and test = result
  or
  result.(ParenExpr).getExpr() = getAnAuthCheck(noError, test)
  or
  result.(NotExpr).getOperand() = getAnAuthCheck(noError.booleanNot(), test)
  or
  result.(LandExpr).getRightOperand() = getAnAuthCheck(noError, test)
  or
  result.(LandExpr).getLeftOperand() = getAnAuthCheck(true, test) and noError = true
  or
  result.(LandExpr).getLeftOperand() = getAnAuthCheck(false, test) and noError = [true, false]
  or
  result.(LorExpr).getRightOperand() = getAnAuthCheck(noError, test)
  or
  result.(LorExpr).getLeftOperand() = getAnAuthCheck(false, test) and noError = false
  or
  result.(LorExpr).getLeftOperand() = getAnAuthCheck(true, test) and noError = [true, false]
}


BlockStmt getErrorBranch(IfStmt ifStmt, boolean polarity) {
  polarity = [true, false] and
  if polarity = true then result = ifStmt.getElse() else result = ifStmt.getThen()
}

from IfStmt i, EqualityTestExpr test
where
test.getAnOperand().(Ident).getName() = "ErrNone"
and not forall(boolean testPolarity |
  i.getCond() = getAnAuthCheck(testPolarity, test) |
  exists(Stmt s | s = getErrorBranch(i, testPolarity).getAStmt() | s instanceof ReturnStmt))
and i.getFile().getBaseName() = "logicalOperators.go"
select i
```

`getAnAuthCheck`æ˜¯é‡ç‚¹

```ql
// ä½¿ç”¨testæ¥çº¦æŸç›´æ¥æ£€æŸ¥çš„ç»“æœ: å¿…é¡»æ˜¯æœ‰ErrNoneå‚ä¸çš„ç­‰å¼æ£€æŸ¥
  result = getADirectAuthCheck(noError) and result = test
or
  // å¯¹getAnAuthCheckçš„ç»“æœè¿›è¡Œçº¦æŸ, ç»“æœå¿…é¡»ä¸ºä¸€ä¸ªParenExprä¸­å»æ‰æ‹¬å·çš„Expr
    // å¦‚æœå­˜åœ¨è¿™æ ·çš„ç»“æœ, é‚£ä¹ˆè¿™é‡Œçš„resultå°±æ»¡è¶³è¦æ±‚äº†
  result.(ParenExpr).getExpr() = getAnAuthCheck(noError, test)
or
  // å¯¹getAnAuthCheckçš„ç»“æœè¿›è¡Œçº¦æŸ, ç»“æœå¿…é¡»ä¸ºä¸€ä¸ªNotExprä¸­å»æ‰Notéƒ¨åˆ†çš„Expr
  result.(NotExpr).getOperand() = getAnAuthCheck(noError.booleanNot(), test)
```

ä½†æ˜¯æ¥ä¸‹æ¥çš„é‚£å‡ ä¸ªANDå’ŒORç›¸å…³çš„å¤„ç†æˆ‘å°±çœ‹ä¸æ‡‚äº†, åç»­å†è¯´å§.

æœ€ååº”è¯¥è¯´æ˜ä¸€ä¸‹`forall`å’Œ`exists`çš„ç»„åˆä½¿ç”¨:

```ql
// æ‰€æœ‰æ»¡è¶³æ±¡ç‚¹å’ŒErrNoneå‚ä¸çš„æ¡ä»¶æ£€æŸ¥ä¸­, æ¯ä¸€ä¸ªåˆ†æ”¯é‡Œéƒ½å­˜åœ¨è¿”å›è¯­å¥
forall(boolean testPolarity |
  i.getCond() = getAnAuthCheck(testPolarity, test) |
// å¦‚æœé”™è¯¯åˆ†æ”¯ä¸­å­˜åœ¨è¿”å›è¯­å¥, é‚£ä¹ˆæˆç«‹
  exists(Stmt s | s = getErrorBranch(i, testPolarity).getAStmt() | s instanceof ReturnStmt))
```

å°†ä¸Šé¢çš„é€»è¾‘å–å, å°±å˜æˆäº† **æ‰€æœ‰æ»¡è¶³æ±¡ç‚¹å’ŒErrNoneå‚ä¸çš„æ¡ä»¶æ£€æŸ¥ä¸­, å­˜åœ¨ä¸åŒ…å«è¿”å›è¯­å¥çš„åˆ†æ”¯**.



### 3.5: Valid returns only

> Ok, so now we make sure we return something when we check the permission. But is that enough? Just returning somehow isn't good enough, we may also need to return an appropriate value. The use of non-nil / nil error values is normal to indicate an error in Go, so letâ€™s assume for this problem that non-nil is considered an appropriate return value. Modify your query to detect all bad examples in checkReturnValue.go.

éœ€è¦æ£€æµ‹é‚£äº›å’ŒErrNoneæ¯”è¾ƒåå¯èƒ½è¿”å›nilçš„æƒ…å†µ.

```ql

import go

class AuthTestConfig extends DataFlow::Configuration {

  AuthTestConfig() { this = "auth-test-config" }

  override predicate isSource(DataFlow::Node source) {
    source = any(DataFlow::CallNode cn |
      cn.getTarget().hasQualifiedName("github.com/minio/minio/cmd", "isReqAuthenticated") or
      // Note new source function:
      cn.getTarget().hasQualifiedName("github.com/github/codeql-ctf-go-return", "errorSource")
    ).getResult()
  }

  override predicate isSink(DataFlow::Node sink) {
    sink = any(DataFlow::EqualityTestNode n).getAnOperand()
  }

}

predicate returnsNil(FuncDecl f) {
  forex(DataFlow::ResultNode r | r.getRoot() = f | r = Builtin::nil().getARead())
}

predicate isNil(Expr e) {
  e = any(CallExpr c | returnsNil(c.getTarget().getFuncDecl())) or
  e = Builtin::nil().getAReference()
}

EqualityTestExpr getADirectAuthCheck(boolean polarity) {
  exists(AuthTestConfig config, DataFlow::Node sink, DataFlow::EqualityTestNode comparison |
    config.hasFlow(_, sink) and comparison.getAnOperand() = sink |
    result = comparison.asExpr() and
    polarity = result.getPolarity()
  )
}

/**
 * Given `ifStmt`'s condition compares some `x` against `ErrNone` with `polarity` (true means checking
 * equality; false checking inequality), return the block reached when `x != ErrNone`.
 */
BlockStmt getErrorBranch(IfStmt ifStmt, boolean polarity) {
  polarity = [true, false] and
  if polarity = true then result = ifStmt.getElse() else result = ifStmt.getThen()
}

from IfStmt i, boolean testPolarity, int resultIdx
where
i.getCond() = getADirectAuthCheck(testPolarity) and
i.getCond().(EqualityTestExpr).getAnOperand().(Ident).getName() = "ErrNone" and
i.getEnclosingFunction().getType().getResultType(resultIdx) = Builtin::error().getType() and // i æ‰€å±å‡½æ•°çš„è¿”å›å€¼ç±»å‹ä¸­æœ‰é”™è¯¯ç±»å‹
not exists(ReturnStmt r |
  r = getErrorBranch(i, testPolarity).getAStmt() |
  not isNil(r.getExpr(resultIdx))
) // è¿™ä¸ªé”™è¯¯ç±»å‹ä¸æ˜¯nil
select i
```
